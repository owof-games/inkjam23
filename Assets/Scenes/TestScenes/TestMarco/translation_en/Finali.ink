=== non_ce_lhai_fatta
DOGRON: {~ The Great Council of Pugs discovered that you cup recipes from. Nigella Lawson|The Supreme Chihuahua has bitten your heel, repeatedly. You will die of rage|Bruiser Woods entered the Grand Council and made Piiiietro win}. THE GOOOOOOOOOD DOG for you ends here.
-> END

=== finale_cinque
-> EliminazioneConcorrente ->
DOGRON: You bring some metaphysical cooking here! What happened when youuuu finished cooking? {~ Pazuzu himself rises from the Underworld to ask you what horror you have cooked|You have cancelled gravity.|A black hole has opened in the control room, let's restart as soon as possible}. I don't know why, but you are undefeatable!
->->

=== finale_quattro
-> EliminazioneConcorrente ->
DOGRON: I'm still not sure if you can cook. How did it go today? {~ Everything is fine in the kitchen. Outside, the universe has been erased| I'm being warned that the planet has stopped turning| Who am I? Where did I come from? What was on my plate? Someone help me}. You have passed today's challenge!
->->

=== finale_tre
-> EliminazioneConcorrente ->
DOGRON: I see your efforts today, and what abouuuuut what you cooked? {~ Out of the oven, the flan lets out heartrending cries|The thing that was on my plate is dancing with the cameraman|You cooked something that can throw me the ball}. You have won this challenge! See you tomorrow!
->->

=== finale_due
-> EliminazioneConcorrente ->
DOGRON: Compared to what you coooooooked... {~ From the frying pan comes a smell of cyanide and sea water|Why is there melted plastic on my plate?|After eating it, I coughed and dropped a lung}. But congratulations: see you in the next episode!
->->

== finale_uno
-> EliminazioneConcorrente ->
DOGRON: Regarding your recipe: {~ What's in the pan doesn't scream, doesn't move, and doesn't ask existential questions|Might be passable for a McDonald's|I know some students who might eat it, maybe}. Unexpectedly move on to the next installment!
->->



=== finale ===
// si usa per il finale del gioco
    ~ moveToEnd()
    // ~ loungeDialogue(DOGRON)
    DOGRON: Uh, look at that beautiful devastation! Did you ever think you would win this program?
        + YOU: Of course, I'm the only person who actually cooked.
        + YOU: You're not going to kill me, are you?
        + YOU: Can I go home now?
        -
    DOGRON: And nowwwww, the prize!
    DOGRON: Surprise: not a restaurant on Mercury, but, but, do you want to hear it?
        + YOU: The longer you take, the less I think I want to know.
        + YOU: Yes, yes, yes!
        + YOU: The important thing is to go home, please.
        -
    DOGRON: You will be the first person!
        + YOU: To do what?
        -
    DOGRON: To replace humans on our planet.
    DOGRON: To take care of the Supreme Chihuahua, the Pugs, and of course a very sweet and very good Labrador like me, patpat!
        + YOU: And what would this labrador be?
        -
    DOGRON: I don't like these jokes, no no.
        + YOU: Who is a good dog? WwwwwHO?
        -
    DOGRON: I am a good dog, a good dog!!!
        + YOU: And where is your home located?
        -
    DOGRON: A very simple planet. There are fire hydrants, gardens, rivers, lots of tennis balls, kennels, pizza trees, and beer fouuuuuuntains. I hope it's to your liking.
        + YOU: Pizza trees?
            DOGRON: Yes. Do you like pizza?
             + + YOU: Let's go!
        + YOU: Beer fountains?
            DOGRON: Unfortunately, they growwwww up that way, even if we don't want to.
            + + YOU: We're leaving, right now!!!
        + YOU: Fire hydrants?
            DOGRON: Yes! Do youuuu like them too? Don't tell me you're a doggo!
            + + YOU: MacchÃ¨ doggo! The Cult is allergic to water, they'll never catch me!
        -
    DOGRON: So, off we go! Our mission here is complete!
    DOGRON: Go with the credits, Bruiser!
-> END


